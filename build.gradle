import org.jetbrains.changelog.ExtensionsKt

plugins {
    id "fr.brouillard.oss.gradle.jgitver" version "0.9.1"
    id "org.jetbrains.changelog" version "1.1.2"
    id "com.diffplug.spotless" version "6.23.3" apply false
}

changelog {
    version = "1.0.0"
    header = { "[${-> project.version}] - ${ExtensionsKt.date('yyyy-MM-dd')}" }
}

subprojects {
    version = 'MANAGED_BY_JGITVER'
    apply plugin: "com.diffplug.spotless"
}

jgitver {
    policy {
        pattern = '([main|develop|support].*)'
        transformations = ['IGNORE']
    }
    policy {
        pattern = '(\\d+\\.\\d+\\.\\d+)'
        transformations = ['IGNORE']
    }
}

allprojects {
    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (project.version.contains("-")) return
            if (details.requested.version.endsWith("-SNAPSHOT")) {
                throw new GradleException("found snapshot dependency")
            }
        }
    }
}
