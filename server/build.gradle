import java.nio.file.Paths
import gg.jte.ContentType

buildscript {
    ext.kotlin_version = '1.8.10' // Required for Kotlin integration
    ext.spring_boot_version = '2.7.8'
    ext.hibernate_version = '5.6.15.Final' // Hibernate 6 is only supported by Spring Boot 3
    ext.jackson_version = '2.14.2'
    ext.kotest_version = '5.5.5'
    ext.jte_version = '2.2.6'
    ext.elastic_version = '7.17.6'
    ext.geotools_version = '28.2'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" // Required for Kotlin integration
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        // See https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "org.hibernate:hibernate-gradle-plugin:$hibernate_version"
    }
}

plugins {
    id 'base'
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.google.cloud.tools.jib' version '3.3.1'
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id 'io.kotest' version '0.3.9'
    id "com.github.ben-manes.versions" version "0.46.0"
    id "gg.jte.gradle" version "$jte_version"
}

apply plugin: 'kotlin' // Required for Kotlin integration
apply plugin: "kotlin-spring" // https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.hibernate.orm'
apply plugin: 'kotlin-noarg'
//apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'

repositories {
    maven {
        url = 'https://nexus.informationgrid.eu/repository/maven-public'
    }
    maven {
        url "https://repo.osgeo.org/repository/release/"
//            url "https://nexus.terrestris.de/repository/public/"
    }
    mavenCentral()
}

configurations {
    webapp
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude group: 'org.springframework.cloud', module: 'spring-cloud-bus'
        exclude group: 'com.sun.mail', module: 'javax.mail'
        exclude group: 'javax.mail', module: 'mailapi'
    }
}

// Profile support
if (!hasProperty('buildProfile') || getProperty('buildProfile') == 'cypress') apply from: "profile-dev.gradle"

// for faster development ignore frontend project and its long build time
if (hasProperty('buildProfile')) {
    dependencies {
        webapp project( ':frontend')
    }

    def unzippedDir = file('build/resources/main').getAbsolutePath()
    task unzip(type:Copy) {
        dependsOn(configurations.webapp)
        into unzippedDir from zipTree(configurations.webapp.singleFile)
    }
    jar.finalizedBy(unzip)

    bootJar {
        dependsOn(unzip)
    }

    bootJarMainClassName {
        dependsOn(unzip)
    }

    compileTestKotlin {
        dependsOn(unzip)
    }

    test {
        dependsOn(unzip)
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version" // Required for Kotlin integration
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.apache.logging.log4j:log4j-api-kotlin:1.2.0'
    implementation 'org.apache.tika:tika-core:2.7.0'
    implementation 'org.glassfish.jersey.media:jersey-media-multipart:3.1.1'

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.pebbletemplates:pebble-spring-boot-starter:3.1.6'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
//    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.security:spring-security-acl'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
//    implementation 'org.slf4j:slf4j-nop:1.7.36'
//    implementation 'org.slf4j:slf4j-api:1.7.36'

    // spring properties metadata
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
//    kapt("org.springframework.boot:spring-boot-configuration-processor")

    // Jackson
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:$jackson_version"

    // JSON schema validation
    implementation 'net.pwall.json:json-kotlin-schema:0.39'

    // Keycloak
    implementation 'org.keycloak:keycloak-spring-boot-starter:19.0.3'
    implementation 'org.keycloak:keycloak-admin-client:19.0.3'

    // Markdown
    implementation "org.commonmark:commonmark:0.21.0"
    implementation "org.commonmark:commonmark-ext-yaml-front-matter:0.21.0"
    implementation "org.commonmark:commonmark-ext-gfm-tables:0.21.0"

    implementation 'org.springdoc:springdoc-openapi-ui:1.6.11' // Attention: also update SwaggerDocumentationConfig.kt
    implementation 'org.springdoc:springdoc-openapi-kotlin:1.6.11'

    // PostgreSQL
    implementation 'org.postgresql:postgresql:42.5.4'
    implementation "org.hibernate:hibernate-core:$hibernate_version"
    implementation 'com.vladmihalcea:hibernate-types-55:2.21.1'

    implementation 'com.github.fge:json-schema-validator:2.2.6'
    implementation('de.ingrid:ingrid-codelist-service:6.0.1') {
        exclude group: 'log4j', module: 'log4j'
    }
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.10.0'
    
    // GeoTools
    implementation("org.geotools.xsd:gt-xsd-gml3:$geotools_version") {
        exclude group: 'org.geotools', module: 'gt-coverage'
        exclude group: 'org.geotools', module: 'gt-metadata'
        exclude group: 'commons-pool', module: 'commons-pool'
        exclude group: 'net.sf.geographiclib', module: 'GeographicLib-Java'
        exclude group: 'javax.media', module: 'jai_core'
        exclude group: 'org.ejml', module: 'ejml-ddense'
        exclude group: 'jgridshift', module: 'jgridshift'
        exclude group: 'org.jdom', module: 'jdom2'
        exclude group: 'org.eclipse.emf', module: 'org.eclipse.emf.ecore.xmi'
        exclude group: 'commons-jxpath', module: 'commons-jxpath'
    }
    implementation("org.geotools:gt-metadata:$geotools_version") {
        exclude group: 'javax.media', module: 'jai_core'
        exclude group: 'commons-pool', module: 'commons-pool'
        exclude group: 'systems.uom', module: 'systems-common-java8'
    }

    // Test
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version" // for kotest framework
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version" // for kotest core jvm assertions
    testImplementation "io.kotest:kotest-assertions-json:$kotest_version" // for comparing json
    testImplementation 'io.kotest.extensions:kotest-extensions-spring:1.1.2' // to wire dependencies using Spring
    testImplementation 'io.mockk:mockk:1.13.4'
    testImplementation "com.ninja-squad:springmockk:4.0.0"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.mockito"
    }
    testImplementation 'org.springframework.security:spring-security-test'

    // iBus communication
    implementation('de.ingrid:ingrid-elasticsearch-tools:6.0.0')

    implementation('de.ingrid:ingrid-ibus-client:6.0.0') {
        exclude group: 'org.apache.logging.log4j'
        exclude group: 'org.slf4j'
    }

    // template engine jte
    implementation "gg.jte:jte:$jte_version"
    implementation "gg.jte:jte-kotlin:$jte_version"
}

dependencyManagement {
    dependencies {
        dependency "org.elasticsearch:elasticsearch:$elastic_version"
        dependency "org.elasticsearch.client:elasticsearch-rest-client:$elastic_version"
        dependency "org.elasticsearch.plugin:transport-netty4-client:$elastic_version"
        dependency "org.elasticsearch.client:transport:$elastic_version"
    }
}

group = 'de.ingrid'
description = 'IGE-NG Server'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

springBoot {
    buildInfo()
}

/*if (hasProperty('filename')) {
    bootJar {
        dependsOn(unzip)
        archiveFileName=getProperty('filename')
    }
}

jar {
    enabled = false

    // This line of code recursively collects and copies all of a project's files
    // and adds them to the JAR itself. One can extend this task, to skip certain
    // files or particular types at will
    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
}*/

allOpen {
    annotation("javax.persistence.Entity")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "17"
    }
}

jte {
    sourceDirectory = Paths.get(project.projectDir.absolutePath, "src", "main", "resources", "templates", "export")
    contentType = ContentType.Plain
    trimControlStructures = true
    precompile()
    generate()
}


tasks.named('compileKotlin') {
    dependsOn(tasks.generateJte)
}


test {
    ignoreFailures = true
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

// create docker image
jib {
    // tag for image on develop branch will be latest
    // tag for image on master branch will be the version
    // tag for image on any feature branch will have the branch name
    def name
    if (project.hasProperty("branch_name")) name = project.ext.branch_name else name = "???"
    def tag = name == "develop" ? "latest" : name
//    def tag = project.ext.branch_name == "develop" ? "latest" : project.ext.branch_name
    tag = tag.replaceAll("/", "-")
    from {
        image = 'eclipse-temurin:17-jre-focal'
    }
    to {
        // in case we build a release we also add release tag from version
        if (!project.version.contains("-")) {
            tags = [version]
        }
        image = 'docker-registry.wemove.com/ingrid-ige-ng:' + tag
    }
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
        entrypoint = ['/run.sh']
    }
    extraDirectories {
        permissions = [
            '/*.sh': '755'
        ]
        paths {
            path {
                from = file('src/main/jib')
            }
            path {
                from = file('build/resources/main/static')
                into = '/app/resources/static'
            }
        }
    }
}

noArg {
    annotation('de.ingrid.igeserver.annotations.NoArgs')
}

hibernate {
    enhance {
        enableLazyInitialization= true
        enableDirtyTracking = true
        enableAssociationManagement = true
        enableExtendedEnhancement = false
    }
}

if (hasProperty('buildDockerImage')) {
    tasks.assemble.dependsOn tasks.jib
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    options.deprecation = true
//    options.compilerArgs << "-Xlint:unchecked"
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}